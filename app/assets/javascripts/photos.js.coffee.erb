# Place all the behaviors and hooks related to the matching controller here.
# All this logic will automatically be available in application.js.
# You can use CoffeeScript in this file: http://coffeescript.org/

class Picker
  constructor: (pickerContainer, @values) ->
    @picker$ = $(pickerContainer)
    @picker$.find('.picker-inc').on 'click', =>
      @inc()
    @picker$.find('.picker-dec').on 'click', =>
      @dec()
  current: =>
    @picker$.find('.picker-val').html()
  inc: =>
    index = $.inArray @current(), @values
    next = @values[(index + 1) % @values.length]
    @update next
  dec: =>
    index = $.inArray @current(), @values
    prev = @values[if index == 0 then @values.length - 1 else index - 1]
    @update prev
  update: (value) =>
    @picker$.find('.picker-val').html value
    @picker$.find('.picker-hidden-val').val value

jQuery ->
  $('.photo').hover(
    ->
      $(this).find('.stats').stop().animate({'opacity': 0.6})
      $(this).find('.thumb-title').stop().animate({'opacity': 0.6})
    -> 
      $(this).find('.stats').stop().animate({'opacity': 0})
      $(this).find('.thumb-title').stop().animate({'opacity': 0})
  )

  <% environment.context_class.instance_eval { include Rails.application.routes.url_helpers } %>
  select2Options = 
    placeholder: 'Add Tags'
    tokenSeparators: [',']
    minimumInputLength: 2
    multiple: true
    containerCssClass: 'tags-picker'
    createSearchChoice: (term) ->
      id: term
      text: term
    ajax:
      url: '/tags'
      dataType: 'json'
      data: (term, page) ->
        q: term
      results: (data, page) ->
        res = []
        $.each data, (i, n) ->
          res.push
            id: i
            text: n
        results: res

  $('.select2:not(#select2-query)').select2 select2Options

  months = ['January', 'February', 'March', 'April', 'May', 'June', 'July', 'August', 'September', 'October', 'November', 'December']
  years = (''+i for i in [1990..2014])

  monthPicker = new Picker '#month-picker', months
  yearPicker  = new Picker '#year-picker', years


  select2SearchOptions =
    placeholder: 'Search by tag'
    multiple: false

  select2QueryOptions = $.extend {}, select2Options, select2SearchOptions
  
  $('#select2-query').select2 select2QueryOptions

  $('#select2-query').on 'change', ->
    #alert 'selected ' + $(this).select2('data')[0].text
    tag = $(this).select2('data').text
    window.location = '/photos?tag=' + encodeURIComponent(tag)

  